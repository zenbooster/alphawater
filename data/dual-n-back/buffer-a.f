// Offsetting alternate rows -- I feel it distributes the effect more,
// but if you prefer more order, comment out the following:
#define OFFSET_ROW


// Standard 2D rotation formula.
mat2 rot2(in float a){ float c = cos(a), s = sin(a); return mat2(c, -s, s, c); }


// IQ's vec2 to float hash.
float hash21(vec2 p){  return fract(sin(dot(p, vec2(27.619, 57.583)))*43758.5453); }


vec2 cellID; // Individual Voronoi cell IDs.


// vec2 to vec2 hash.
vec2 hash22B(vec2 p) { 

    // Faster, but doesn't disperse things quite as nicely. However, when framerate
    // is an issue, and it often is, this is a good one to use. Basically, it's a tweaked 
    // amalgamation I put together, based on a couple of other random algorithms I've 
    // seen around... so use it with caution, because I make a tonne of mistakes. :)
    float n = sin(dot(p, vec2(41, 289)));
    return fract(vec2(262144, 32768)*n)*2. - 1.; 
    //return sin( p*6.2831853 + iTime ); 
}

// Based on IQ's gradient noise formula.
float n2D3G( in vec2 p ){
   
    // Cell ID and local coordinates.
    vec2 i = floor(p); p -= i;
    
    // Four corner samples.
    vec4 v;
    v.x = dot(hash22B(i), p);
    v.y = dot(hash22B(i + vec2(1, 0)), p - vec2(1, 0));
    v.z = dot(hash22B(i + vec2(0, 1)), p - vec2(0, 1));
    v.w = dot(hash22B(i + 1.), p - 1.);

    // Cubic interpolation.
    p = p*p*(3. - 2.*p);
    
    // Bilinear interpolation -- Along X, along Y, then mix.
    return mix(mix(v.x, v.y, p.x), mix(v.z, v.w, p.x), p.y);
    
}

// Two layers of noise.
float fBm(vec2 p){ return n2D3G(p)*.66 + n2D3G(p*2.)*.34; }


float bMap(vec2 p){
    
    // Put the grid on an angle to interact with the light a little better.
    p *= rot2(-3.14159/5.);
   
    #ifdef OFFSET_ROW
    // Tacky way to construct an offset square grid.
    if(mod(floor(p.y), 2.)<.5) p.x += .5;
    #endif

    
    // Cell ID and local coordinates.
    vec2 ip = floor(p);
    p -= ip + .5;
    
    // Recording the cell ID.
    cellID = ip;

    // Transcendental angle function... Made up on the spot.
    //float ang = dot(sin(ip/4. - cos(ip.yx/2. + iTime))*6.2831, vec2(.5));
    
    // Noise function. I've rotated the point around a bit so that the 
    // objects hang down due to gravity at the zero mark.
    float ang = -3.14159*3./5. + (fBm(ip/8. + iTime/3.))*6.2831*2.;
    // Offset point within the cell. You could increase this to cell edges
    // (.5), but it starts to look a little weird at that point.
    vec2 offs = vec2(cos(ang), sin(ang))*.35;
     
    // Linear pyramid shading, according to the offset point. Basically, you
    // want a value of zero at the edges and a linear increase to one at the 
    // offset point peak. As you can see, I've just hacked in something quick 
    // that works, but there'd be more elegant ways to achieve the same.
    if(p.x<offs.x)  p.x = 1. - (p.x + .5)/abs(offs.x  + .5);
    else p.x = (p.x - offs.x)/(.5 - offs.x);

    if(p.y<offs.y) p.y = 1. - (p.y + .5)/abs(offs.y + .5);
    else p.y = (p.y - offs.y)/(.5 - offs.y);

    // Return the offset pyramid distance field. Range: [0, 1].
    return 1. - max(p.x, p.y);
}


// Standard function-based bump mapping function, with an edge value 
// included for good measure.
vec3 doBumpMap(in vec2 p, in vec3 n, float bumpfactor, inout float edge){
    
    // Sample difference. Usually, you'd have different ones for the gradient
    // and the edges, but we're finding a happy medium to save cycles.
    vec2 e = vec2(.025, 0);
    
    float f = bMap(p); // Bump function sample.
    float fx = bMap(p - e.xy); // Same for the nearby sample in the X-direction.
    float fy = bMap(p - e.yx); // Same for the nearby sample in the Y-direction.
    float fx2 = bMap(p + e.xy); // Same for the nearby sample in the X-direction.
    float fy2 = bMap(p + e.yx); // Same for the nearby sample in the Y-direction.
    
    vec3 grad = (vec3(fx - fx2, fy - fx2, 0))/e.x/2.;   
    
    // Edge value: There's probably all kinds of ways to do it, but this will do.
    edge = length(vec2(fx, fy) + vec2(fx2, fy2) - f*2.);
    //edge = (fx + fy + fx2 + fy2 - f*4.);
    //edge = abs(fx + fx2 - f*2.) + abs(fy + fy2 - f*2.);
    //edge /= e.x;
    edge = smoothstep(0., 1., edge/e.x);
     
    // Applying the bump function gradient to the surface normal.
    grad -= n*dot(n, grad);          
    
    // Return the normalized bumped normal.
    return normalize( n + grad*bumpfactor );
	
}


// A hatch-like algorithm, or a stipple... or some kind of textured pattern.
float doHatch(vec2 p, float res){
    
    
    // The pattern is physically based, so needs to factor in screen resolution.
    p *= res/16.;

    // Random looking diagonal hatch lines.
    float hatch = clamp(sin((p.x - p.y)*3.14159*200.)*2. + .5, 0., 1.); // Diagonal lines.

    // Slight randomization of the diagonal lines, but the trick is to do it with
    // tiny squares instead of pixels.
    float hRnd = hash21(floor(p*6.) + .73);
    if(hRnd>.66) hatch = hRnd;  


    return hatch;

    
}

void mainImage(out vec4 fragColor, in vec2 fragCoord){

    // Resolution and aspect correct screen coordinates.
    float iRes = min(iResolution.y, 800.);
    vec2 uv = (fragCoord - iResolution.xy*.5)/iRes;
    
    // Unit direction vector. Used for some mock lighting.
    vec3 rd = normalize(vec3(uv, .5));
    
    // Scaling and tranlation.
    const float gSc = 10.;
    vec2 p = uv*gSc + vec2(0, iTime/2.);
    vec2 oP = p; // Saving a copy for later.
    
    
    // Take a function sample.
    float m = bMap(p);
    
    vec2 svID = cellID;
  
    // Face normal for and XY plane sticking out of the screen.
    vec3 n = vec3(0, 0, -1);
    
    // Bump mapping the normal and obtaining an edge value.
    float edge = 0., bumpFactor = .25;
    n = doBumpMap(p, n, bumpFactor, edge);
   
    // Light postion, sitting back from the plane and animated slightly.
	vec3 lp =  vec3(-0. + sin(iTime)*.3, .0 + cos(iTime*1.3)*.3, -1) - vec3(uv, 0);
    
    // Liight distance and normalizing.
    float lDist = max(length(lp), .001);
    vec3 ld = lp/lDist;
    // Unidirectional lighting -- Sometimes, it looks nicer.
    //vec3 ld = normalize(vec3(-.3 + sin(iTime)*.3, .5 + cos(iTime*1.3)*.2, -1));
	
	// Diffuse, specular and Fresnel.
	float diff = max(dot(n, ld), 0.);
    diff = pow(diff, 4.);
    float spec = pow(max(dot(reflect(-ld, n), -rd), 0.), 16.);
	// Fresnel term. Good for giving a surface a bit of a reflective glow.
    float fre = min(pow(max(1. + dot(n, rd), 0.), 4.), 3.);
    

    // Applying the lighting.
    vec3 col = vec3(.15)*(diff + .251 + spec*vec3(1, .7, .3)*9. + fre*vec3(.1, .3, 1)*12.);
    
    
    // Some dodgy fake reflections. This was made up on the fly. It's no sustitute for reflecting
    // into a proper back scene, but it's only here to add some subtle red colors.
    float rf = smoothstep(0., .35, bMap(reflect(rd, n).xy*2.)*fBm(reflect(rd, n).xy*3.) + .1);
    col += col*col*rf*rf*vec3(1, .1, .1)*15.;
    
    /*
    // Random blinking lights. Needs work. :)
    float rnd = hash21(svID);
    float rnd2 = hash21(svID + .7);
    rnd = sin(rnd*6.2831 + iTime*1.);
    col *= mix(vec3(1), (.5 + .4*cos(6.2831*rnd2 + vec3(0, 1, 2)))*6., smoothstep(.96, .99, rnd));
    */
    
     // Using the distance function value for some faux shading.
    float shade = m*.83 + .17;
    col *= shade;
    
    // Apply the edging from the bump function. In some situations, this can add an
    // extra touch of dimension. It's so easy to apply that I'm not sure why people 
    // don't use it more. Bump mapped edging works in 3D as well.
    col *= 1. - edge*.8;
    
    // Apply a cheap but effective hatch function.
    float hatch = doHatch(oP/gSc, iRes);
    col *= hatch*.5 + .7;
    
    // Just the distance function.
    //col = vec3(m);

/*
    // Adding in a couple of fake spot lights.
    // Interesting... but not for this example. :)
    float x = iResolution.x/iResolution.y/2.;
    vec2 uv2 = uv - vec2(-x, .5);
    uv2 *= rot2(cos(iTime)*.5 - .5);
    float light = abs(uv2.x) - .25*(-uv2.y*1.5);
    float atten = 1. - 1./(1. + pow(max(uv2.y*.5 + .5, 0.), 2.)*16.);
    col = mix(col, col*vec3(1, .3, .1)*8., (1. - smoothstep(0., .05, light))*atten);
 
    uv2 = uv - vec2(x, .5); 
    uv2 *= rot2(sin(-iTime)*.5 + .5);
    light = abs(uv2.x) - .25*(-uv2.y*1.5);
    atten = 1. - 1./(1. + pow(max(uv2.y*.5 + .5, 0.), 2.)*16.);
    col = mix(col, col*vec3(.1, .4, 1)*8., (1. - smoothstep(0., .05, light))*atten);
*/  
  
    // Subtle vignette.
    //uv = fragCoord/iResolution.xy;
    //col *= pow(16.*uv.x*uv.y*(1. - uv.x)*(1. - uv.y) , .125);
    // Colored variation.
    //col = mix(col*vec3(.25, .5, 1)/8., col, pow(16.*uv.x*uv.y*(1. - uv.x)*(1. - uv.y) , .125));
    

	fragColor = vec4(sqrt(max(col, 0.)), 1);
}